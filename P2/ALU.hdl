/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    // x ó 0
    Mux16(a=x, b=false, sel=zx, out=outzx);
    Not16(in=outzx, out=noutzx);
    // x ó notx
    Mux16(a=outzx, b=noutzx, sel=nx, out=outnx);

    // y ó 0
    Mux16(a=y, b=false, sel=zy, out=outzy);
    Not16(in=outzy, out=noutzy);
    // y ó noty
    Mux16(a=outzy, b=noutzy, sel=ny, out=outny);

    // Input para f
    And16(a=outnx, b=outny, out=outand);
    Add16(a=outnx, b=outny, out=outadd);
    // x+y ó x&y
    Mux16(a=outand, b=outadd, sel=f, out=outf);
    Not16(in=outf, out=noutf);
    // out ó !out y ng
    Mux16(a=outf, b=noutf, sel=no, out=out, 
                                   out[15]=ng, 
                                   out[0..7]=aout, 
                                   out[8..15]=bout);
    // Input para out==0
    Or8Way(in=aout, out=faout);
    Or8Way(in=bout, out=fbout);
    // out==0?
    Or(a=faout, b=fbout, out=oout);
    Not(in=oout, out=zr);




}